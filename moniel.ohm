Moniel{
    Network
        = Definition*
    Definition
        = (BlockDefinition | ConnectionDefinition)

    ConnectionDefinition
        = NonemptyListOf<(BlockList | Block), connectionSymbol>

    BlockList (block list)
        = "[" ListOf<Block, ","> "]"

    Block
        = BlockInstance | identifier

    BlockInstance (block)
        = LayerIdentifier blockName BlockInstanceParameters
    BlockInstanceParameters
        = ("(" Parameters ")")?

    BlockDefinition
        = "+" blockName BlockDefinitionParameters BlockDefinitionBody
    BlockDefinitionParameters
        = ("(" Parameters ")")?
    BlockDefinitionBody
        = ("{" Definition* "}")?

    Parameters
        = ListOf<Parameter, ",">
    Parameter (parameter)
        =  parameterName "=" (ValueList | Value)

    ValueList
        = "<" ListOf<Value, ";"> ">"
    Value
        = blockName | Dimension

    LayerIdentifier
        = (identifier ":")?
        
    blockName (block name)
        = upper letter*
    identifier (identifier)
        = lower alnum*
    parameterName
        = letter+
    number
        = digit+
    Dimension
        = ListOf<number, "x">
    connectionSymbol (->)
        = "->"
    space
        += comment

    comment
        = "//" (~"\n" any)* "\n"  -- singleLine
        | "/*" (~"*/" any)* "*/"  -- multiLine
}