Moniel{
    Graph
        = Definition*
    Definition
        = (NodeDefinition | Chain)

    Chain
        = NonemptyListOf<(List | Block), edge>

    List
        = "[" ListOf<ListItem, ","> "]"
    ListItem
        = Block

    Block
        = Node | InlineMetanode | path

    Node
        = BlockName nodeType BlockParameters

    BlockName
        = (identifier ":")?

    InlineMetanode
        = BlockName Metanode
    NodeDefinition
        = "+" nodeType BlockParameters Metanode?
    BlockParameters
        = ("(" Parameters ")")?
    Metanode
        = ("{" Graph "}")


    Parameters
        = ListOf<Parameter, ",">
    Parameter (parameter)
        =  parameterName "=" Value
    Value
        = parameterName | nodeType | float | Shape | integer

    nodeType
        =  &upper (alnum | "_")+
    identifier
        =  &lower (alnum | "_")+
    path (path)
        = nonemptyListOf<identifier, "/">
    parameterName
        = letter+
    integer
        = digit+
    float
        = digit* "." digit+
    Shape
        = ListOf<integer, "x">
    edge (->)
        = "->"
    space
        += comment
    comment
        = "//" (~"\n" any)* "\n"?   -- singleLine
        | "/*" (~"*/" any)* "*/"    -- multiLine
}