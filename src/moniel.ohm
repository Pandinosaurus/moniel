Moniel{
    Network
        = Definition*
    Definition
        = (BlockDefinition | ScopeDefinition | ConnectionDefinition)

    ConnectionDefinition
        = NonemptyListOf<(BlockList | Block), connectionSymbol>

    BlockList (block list)
        = "[" ListOf<Block, ","> "]"

    Block
        = BlockInstance | blockIdentifier

    BlockInstance (block)
        = BlockName blockType BlockInstanceParameters
    BlockInstanceParameters
        = ("(" Parameters ")")?

    BlockName
        = (blockName ":")?

    BlockDefinition
        = "+" blockType BlockDefinitionParameters BlockDefinitionBody
    BlockDefinitionParameters
        = ("(" Parameters ")")?
    BlockDefinitionBody
        = ("{" Definition* "}")?

    ScopeDefinition
        = "/" scopeName ScopeDefinitionBody
    ScopeDefinitionBody
        = ("{" Definition* "}")?

    Parameters
        = ListOf<Parameter, ",">
    Parameter (parameter)
        =  parameterName "=" (ValueList | Value)

    ValueList
        = "<" ListOf<Value, ";"> ">"
    Value
        = parameterName | blockType | float | Shape | integer

    scopeName
        = lower (alnum | "_")*
    blockType (block name)
        = upper (alnum | "_")*
    blockName
        = lower (alnum | "_")*
    blockIdentifier (identifier)
        = "./"? lower (alnum | "_" | "/")*
    parameterName
        = letter+
    integer
        = digit+
    float
        = digit* "." digit+
    Shape
        = ListOf<integer, "x">
    connectionSymbol (->)
        = "->"
    space
        += comment

    comment
        = "//" (~"\n" any)* "\n"?   -- singleLine
        | "/*" (~"*/" any)* "*/"    -- multiLine
}